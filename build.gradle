buildscript {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://mirrors.huaweicloud.com/repository/maven/' }
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6"
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'application'


    group 'com.jean.demo'
    version '1.0'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://mirrors.huaweicloud.com/repository/maven/' }
        mavenCentral()
    }

}


subprojects {

    /**
     * GIT信息
     */
    apply plugin: "com.gorylenko.gradle-git-properties"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'docker'
    apply plugin: "org.sonarqube"

    ext {
        mysql = "5.1.41"
        swagger = "2.7.0"
        druid = "1.1.5"
        lombok = "1.16.18"
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Dalston.SR4'
        }
    }

    gitProperties {
        dateFormat = "yyyy-MM-dd HH:mm:ss"
        dateFormatTimeZone = "GMT+8:00"
    }

    docker {
        baseImage = "openjdk:latest"
        maintainer = 'jinshubao jinshubao123@126.com'
        registry ='jinshubao'
    }

    distDocker {
        push = false
        exposePort(8080)
        setEnvironment('JAVA_OPTS', '-Xms128m -Xmx256m -Djava.security.egd=file:/dev/./urandom')
        volume("/tmp")
        volume("/logs")
    }

    sonarqube{
        properties{
            property "sonar.host.url", "http://10.52.2.203:10001/"
        }
    }

    /**
     * 扩展application.yml属性，从gradle读取，
     */
    processResources {
        expand(project.properties)
    }

}
